title:: SerialOSCClient Clip Launcher Example
summary:: Notes on using the SerialOSCClient Clip Launcher Example
categories:: External Control
related:: Guides/UsingSerialOSCClient

section::SerialOSCClient Clip Launcher Example

code::
(
~clip = SerialOSCClient.grid('clip') { |client|
	var playingLedLevel = 15, queuedLedLevel = 8, loadedLedLevel = 4;
	var numTracks = 15;
	var numSlotsPerTrack = 15;
	var quant;
	var slice = 0.01; // slice must be less than quant
	var listeners;

	var patterns = Array.fill2D(numTracks, numSlotsPerTrack);
	var protoEvents = Array.fill2D(numTracks, numSlotsPerTrack);
	var players = Array.fill2D(numTracks, numSlotsPerTrack);
	var queueRoutines = Array.fill(numTracks);
	var queuedSlots = Array.fill(numTracks);
	var playingSlots = Array.fill(numTracks);

	var numVisibleTracks = { min(numTracks, client.grid.numCols-1) };

	var setQuant = { |beats|
		if (beats.notNil) {
			quant = [beats, (-1) * slice];
		} {
			quant = slice;
		};
	};

	var playSlot = { |trackIndex, slotIndex|
		patterns[trackIndex][slotIndex].notNil.if {
			stopQueued.value(trackIndex);
			queueSlot.value(trackIndex, slotIndex);
			refreshTrackLeds.value(trackIndex);
		} {
			stopTrack.value(trackIndex);
		}
	};

	var queueSlot = { |trackIndex, slotIndex|
		var routine = Routine {
			var pattern;
			var player;
			var stopListener;
			pattern = patterns[trackIndex][slotIndex];
			stopListener = { |player, what|
			case
			{ [\stopped, \userStopped].includes(what) } {
				player.removeDependant(stopListener);
				playingSlots[trackIndex] = nil;
				refreshTrackLeds.value(trackIndex);
			}
			};

			playingSlots[trackIndex].notNil.if {
				var slotIndex = playingSlots[trackIndex];
				players[trackIndex][slotIndex].stop;
			};

			slice.wait;

			player = pattern.play(protoEvent: protoEvents[trackIndex][slotIndex]); // TODO: remove protoEvent since this addition makes Clip less generic
			player.addDependant(stopListener);

			players[trackIndex][slotIndex] = player;
			queuedSlots[trackIndex] = nil;
			playingSlots[trackIndex] = slotIndex;
			refreshTrackLeds.value(trackIndex);
		};

		queueRoutines[trackIndex] = routine;
		routine.play(quant: quant);
		queuedSlots[trackIndex] = slotIndex;
	};

	var refreshTrackLeds = { |trackIndex|
		client.grid !? { |grid|
			if (trackIndex < (grid.numCols-1)) {
				(grid.numRows-1).do { |slotIndex|
					var level = if (slotIndex <= numSlotsPerTrack) {
						case
						{ playingSlots[trackIndex] == slotIndex } { playingLedLevel }
						{ queuedSlots[trackIndex].value == slotIndex } { queuedLedLevel }
						{ patterns[trackIndex][slotIndex].notNil } { loadedLedLevel }
						} ? 0;
					client.ledLevelSet(trackIndex, slotIndex, level);
				};
			};
		};
	};

	var stopTrack = { |trackIndex|
		stopQueued.value(trackIndex);
		stopPlaying.value(trackIndex);
	};

	var stopQueued = { |trackIndex|
		queuedSlots[trackIndex] !? { |slotIndex|
			queueRoutines[trackIndex].stop;
			queuedSlots[trackIndex] = nil;
		};
	};

	var stopPlaying = { |trackIndex|
		playingSlots[trackIndex] !? { |slotIndex|
			var routine = Routine { // TODO: possibly need to save this routine to not interfer with new queued play action of same slot
				players[trackIndex][slotIndex].stop;
			};
			routine.play(quant: quant);
		};
	};

	// client.gridRotationChangedAction = { |client| }; // TODO: not sure if anything is needed here, assuming gridRefreshAction is triggered?

	client.onGridRouted = { |client| }; // TODO: check client is argument in SerialOSCClient

	client.onFree = { numTracks.do { |i| stopTrack.value(i) } };

	client.addUniqueMethod(\load) { |client, trackIndex, slotIndex, pattern|
		stopPlaying.value(trackIndex);
		patterns[trackIndex][slotIndex] = pattern;
		client.grid !? { |grid|
			if ( (trackIndex < (grid.numCols-1)) and: (slotIndex < (grid.numRows-1)) ) {
				client.ledLevelSet(trackIndex, slotIndex, loadedLedLevel);
			};
		};
	};

	client.addUniqueMethod(\loadProtoEvent) { |client, trackIndex, slotIndex, event|
		stopPlaying.value(trackIndex);
		protoEvents[trackIndex][slotIndex] = event;
	};

	client.addUniqueMethod(\quant_) { |client, beats| setQuant.value(beats) };

	setQuant.value(1);

	// TODO s.waitForBoot { // not really needed
		client.gridKeyAction = { |client, x, y, state, timestamp|
			case
			{ state.asBoolean and: (y < (client.grid.numRows-1)) } {
				case
				{ x < numVisibleTracks.value } { playSlot.value(x, y) }
				{ x == (client.grid.numCols-1) } {
					numVisibleTracks.value.do { |trackIndex| playSlot.value(trackIndex, y) }
				}
			}
			{ state.asBoolean and: (y == (client.grid.numRows-1))} {
				case
				{ x < numVisibleTracks.value } { stopTrack.value(x) }
				{ x == (client.grid.numCols-1) } {
					numVisibleTracks.value.do { |trackIndex| stopTrack.value(trackIndex) }
				}
			};
		};

		client.gridRefreshAction = { |client| // TODO: check client is argument in SerialOSCClient
			numVisibleTracks.value.do { |trackIndex| refreshTrackLeds.value(trackIndex) };
		};

		client.addUniqueMethod(\play) { |client, trackIndex, slotIndex| playSlot.value(trackIndex, slotIndex) };

		client.grid !? { client.refreshGrid };
	// TODO }
};
)
::

section::Using Clip

subsection::How it works

The Clip Launcher example app triggers playable things (typically, patterns) in a number of tracks on quantized divisions akin to how clips are triggered in the Session view of Ableton Live.

subsection::Loading a Clip

code::
(
~clip.load(
	0, // first track
	0, // first clip
	Pbind(*[
		degree: Pseq([0, 5, 2, 12], inf),
		octave: Pseq([Pn(5, 4), Pn(4, 4)], inf),
		dur: 0.2
	])
);
)
::

subsection::Changing quantization

code::
~clip.quant; // current quant

~clip.quant = 4; // change quant
~clip.quant = 2;
~clip.quant = nil; // minimal quant
::

subsection::Creating Clip variations with proto Events

code::
(
SynthDef('tick', { |out=0, freq, amp|
    var sig;
    sig = SinOsc.ar(freq) * EnvGen.ar(Env.perc(0.01, 0.2), doneAction: 2) * amp;
    OffsetOut.ar(out, sig ! 2);
}).add;
)

(
~metro = Pbind(*[
    instrument: \tick,
    octave: Pseq([6, 5, 5, 5], inf)
]);
)

7.do { |i| ~clip.load(6, i, ~metro) };
~clip.loadProtoEvent(6, 1, (mtranspose: 2));
~clip.loadProtoEvent(6, 2, (mtranspose: -12));
~clip.loadProtoEvent(6, 3, (mtranspose: -12, dur: 0.125));
~clip.loadProtoEvent(6, 4, (mtranspose: 12, dur: 0.125));
::

subsection::Hacking Clip

code::
// GUI loops using a Task ?
// Play sound ?
// Loop sound ?
// Recording looper ?
::

subsection::What can be triggered

The short answer: link::Classes/Pattern::s and link::Classes/Task::s.

But anything that responds to the same methods and adheres to the same principles of Patterns will work.

A more elaborate answer: Any object that responds to a method code::.play:: that takes quant, this, and that (TODO) as arguments and that starts something and in turn returns an object that responds to method code::.stop:: in order to stop that something suffices as a clip to be launched. In addition, if that something may stop itself and the object that code::.play:: returns notifies dependants by invoking code::.changed:: with the first argument set to 'stopped' or 'userStopped' the clip launcher will automatically track the clip state and update leds accordingly.

section::Considerations

Server latency affects response time.

code::
s.latency; // check current server latency
s.latency = 0.05; // set a low server latency
::

... as will soundcard latency:

code::
s.options.hardwareBufferSize; // sound card latency

(
// reboot with really low sound card latency (if possible)
s.options.hardwareBufferSize = 64;
s.reboot
)
::


subsection::TODO

code::
(
SynthDef('recorder', { |bufnum|
	RecordBuf.ar(SoundIn.ar([0, 1]), bufnum);
}).add;
SynthDef('player', { |out=0, bufnum|
    var playbuf;
    playbuf = PlayBuf.ar(2, bufnum);
    //FreeSelfWhenDone.kr(playbuf); // frees the synth when the PlayBuf is finished
    Out.ar(out, playbuf);
}).add;
)

(
s.waitForBoot {
	~spawnLooper = {
		var buffer, looper;
		buffer = Buffer.alloc(s, 44100*8, 2);
		looper = (recordingDone: false);
		looper.playClip = { |self|
			s.bind {
			if (looper[\recordingDone]) {
				looper[\synth] = Synth.new('player', [\bufnum, buffer]);
			} {
				looper[\synth] = Synth.new('recorder', [\bufnum, buffer]);
			};
			};
			looper;
		};
		looper.stopClip = { |self|
			s.bind {
			looper[\synth].free;
			looper[\recordingDone].not.if {
				looper[\recordingDone] = true;
			};
			};
			looper.changed(\stopped);
		};
	};
};
)

~clip.load(0, 0, ~spawnLooper.value);
~clip.load(0, 1, ~spawnLooper.value);
~clip.load(0, 2, ~spawnLooper.value);
~clip.load(0, 3, ~spawnLooper.value);

~clip.load(1, 0, ~spawnLooper.value);


Routine

~buffer.plot

~looper[\playClip].value;
~looper[\stopClip].value;
::

code::
SerialOSCClient.init(legacyMode: true);
(
~clip = SerialOSCClient.grid('clip') { |client|
    var playingLedLevel = 15;
    var queuedLedLevel = 6;
    var loadedLedLevel = 3;
    var numTracks = 15;
    var numSlotsPerTrack = 15;
    var quant = 1;
    var slice = 0.01; // slice must be less than quant
    var listeners;

    var patterns = Array.fill2D(numTracks, numSlotsPerTrack);
    var players = Array.fill2D(numTracks, numSlotsPerTrack);
    var queueRoutines = Array.fill(numTracks);
    var queuedSlots = Array.fill(numTracks);
    var playingSlots = Array.fill(numTracks);

    var numVisibleTracks = { min(numTracks, client.grid.getNumCols-1) }; // TODO: rename getNumCols in SerialOSCClient to numCols

	var playit = { |clip|
		if (clip.respondsTo(\at)) {
			clip[\playClip].value;
		} {
			clip.play;
		};
	};

	var stopit = { |clip|
		if (clip.respondsTo(\at)) {
			clip[\stopClip].value;
		} {
			clip.stop;
		};
	};

    var playSlot = { |trackIndex, slotIndex|
        patterns[trackIndex][slotIndex].notNil.if {
            stopQueued.value(trackIndex);
            queueSlot.value(trackIndex, slotIndex);
            refreshTrackLeds.value(trackIndex);
        } {
            stopTrack.value(trackIndex);
        }
    };

    var queueSlot = { |trackIndex, slotIndex|
        var routine = Routine {
            var pattern;
            var player;
            var stopListener;
            pattern = patterns[trackIndex][slotIndex];
            stopListener = { |player, what|
            case
            { [\stopped, \userStopped].includes(what) } {
                player.removeDependant(stopListener);
                playingSlots[trackIndex] = nil;
                refreshTrackLeds.value(trackIndex);
            }
            };

            playingSlots[trackIndex].notNil.if {
                var slotIndex = playingSlots[trackIndex];
                // players[trackIndex][slotIndex].stop;
				stopit.value(players[trackIndex][slotIndex]);
            };

            slice.wait;

			// player = pattern.play;
			player = playit.value(pattern);
            player.addDependant(stopListener);

            players[trackIndex][slotIndex] = player;
            queuedSlots[trackIndex] = nil;
            playingSlots[trackIndex] = slotIndex;
            refreshTrackLeds.value(trackIndex);
        };

        queueRoutines[trackIndex] = routine;
        routine.play(quant: quant);
        queuedSlots[trackIndex] = slotIndex;
    };

    var refreshTrackLeds = { |trackIndex|
        client.grid !? { |grid|
            if (trackIndex < (grid.getNumCols-1)) {
                (grid.getNumRows-1).do { |slotIndex| // TODO: rename getNumRows to numRows in SerialOSCClient
                    var level = if (slotIndex <= numSlotsPerTrack) {
                        case
                        { playingSlots[trackIndex] == slotIndex } { playingLedLevel }
                        { queuedSlots[trackIndex].value == slotIndex } { queuedLedLevel }
                        { patterns[trackIndex][slotIndex].notNil } { loadedLedLevel }
                        } ? 0;
                    client.ledLevelSet(trackIndex, slotIndex, level);
                };
            };
        };
    };

    var stopTrack = { |trackIndex|
        stopQueued.value(trackIndex);
        stopPlaying.value(trackIndex);
    };

    var stopQueued = { |trackIndex|
        queuedSlots[trackIndex] !? { |slotIndex|
            queueRoutines[trackIndex].stop;
            queuedSlots[trackIndex] = nil;
        };
    };

    var stopPlaying = { |trackIndex|
        playingSlots[trackIndex] !? { |slotIndex|
            var routine = Routine { // TODO: possibly need to save this routine to not interfer with new queued play action of same slot
                // players[trackIndex][slotIndex].stop;
				stopit.value(players[trackIndex][slotIndex]);
            };
            routine.play(quant: quant);
        };
    };

    // client.gridRotationChangedAction = { |client| }; // TODO: not sure if anything is needed here, assuming gridRefreshAction is triggered?

    client.onGridRouted = { |client| }; // TODO: check client is argument in SerialOSCClient

    client.onFree = { numTracks.do { |i| stopTrack.value(i) } };

    client.addUniqueMethod(\load) { |client, trackIndex, slotIndex, pattern|
        stopPlaying.value(trackIndex);
        patterns[trackIndex][slotIndex] = pattern;
        client.grid !? { |grid|
            if ( (trackIndex < (grid.getNumCols-1)) and: (slotIndex < (grid.getNumRows-1)) ) {
                client.ledLevelSet(trackIndex, slotIndex, loadedLedLevel);
            };
        };
    };

    client.addUniqueMethod(\quant_) { |client, argQuant| quant = argQuant };

    s.waitForBoot {
        client.gridKeyAction = { |client, x, y, state, timestamp|
            case
            { state.asBoolean and: (y < (client.grid.getNumRows-1)) } {
                case
                { x < numVisibleTracks.value } { playSlot.value(x, y) }
                { x == (client.grid.getNumCols-1) } {
                    numVisibleTracks.value.do { |trackIndex| playSlot.value(trackIndex, y) }
                }
            }
            { state.asBoolean and: (y == (client.grid.getNumRows-1))} {
                case
                { x < numVisibleTracks.value } { stopTrack.value(x) }
                { x == (client.grid.getNumCols-1) } {
                    numVisibleTracks.value.do { |trackIndex| stopTrack.value(trackIndex) }
                }
            };
        };

        client.gridRefreshAction = { |client| // TODO: check client is argument in SerialOSCClient
            numVisibleTracks.value.do { |trackIndex| refreshTrackLeds.value(trackIndex) };
        };

        client.addUniqueMethod(\play) { |client, trackIndex, slotIndex| playSlot.value(trackIndex, slotIndex) };

        client.grid !? { client.refreshGrid };
    }
};
)
::


code::
// class Looper
Looper {
	var seconds;
	var buffer;
	var synth;
	var recordingDone=false;
	var sampleRate = 44100;

	*new { |seconds=4|
		^super.new.init(seconds);
	}

	init { |argSeconds|
		seconds = argSeconds;
		this.addSynthDefs;
		Server.default.waitForBoot {
			buffer = Buffer.alloc(Server.default, sampleRate*seconds, 2);
		}
	}

	addSynthDefs {
		SynthDef('recorder', { |bufnum|
			RecordBuf.ar(SoundIn.ar([0, 1]), bufnum);
		}).add;
		SynthDef('player', { |out=0, bufnum|
			var playbuf;
			playbuf = PlayBuf.ar(2, bufnum);
			//FreeSelfWhenDone.kr(playbuf);
			OffsetOut.ar(out, playbuf);
		}).add;
	}

	play { |...args|
		Routine {
			Server.default.bind {
				if (recordingDone) {
					synth = Synth.new('player', [\bufnum, buffer]);
				} {
					synth = Synth.new('recorder', [\bufnum, buffer]);
				};
			};
		}.play(*args);
	}

	stop {
		synth.free;
		recordingDone.not.if {
			recordingDone = true;
		};
		this.changed(\stopped);
	}
}

::

code::
(
SerialOSCClient.init(legacyMode: true);
s.latency=0.02;
//s.options.device = "Behr";
//s.options.device = "USB AUDIO  CODEC";
s.options.device = "BB";
s.options.hardwareBufferSize=64;
s.reboot;
TempoClock.tempo = 120/60
)

~clip.quant_(4);

~clip.load(0, 0, Looper.new);
~clip.load(0, 1, Looper.new);

~clip.load(1, 0, Looper.new);
~clip.load(1, 1, Looper.new);


(
SynthDef('tick', { |out=0, freq, amp|
	var sig;
	sig = SinOsc.ar(freq) * EnvGen.ar(Env.perc(0.01, 0.2), doneAction: 2) * amp;
	OffsetOut.ar(out, sig ! 2);
}).add;
)

(
~metro = Pbind(*[
	instrument: \tick,
	midinote: Pseq([80, 68, 68, 68], inf),
	dur: 1
]);
)

~metro.play;
~clip.load(6, 0, ~metro);

~monitor={Out.ar(0, SoundIn.ar([0, 1]))}.play;
~monitor.free;
::
